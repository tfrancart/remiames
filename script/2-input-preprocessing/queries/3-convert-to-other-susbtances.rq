PREFIX ruim: <http://data.esante.gouv.fr/ansm/medicament/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX r: <http://www.limics.org/remiames_ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?ID_patient ?ID_sejour ?date ?heure ?UCD ?quantite ?unite_de_prise ?labelUCD (?code_substance_target AS ?codeSubstance) ?quantiteSubstanceTotale ?unite
WHERE{ 

	# Lire les administrations de substance, contenant les codes substances
	# issus du RUIM
	{
		SELECT ?ID_patient ?ID_sejour ?date ?heure ?UCD ?quantite ?unite_de_prise ?labelUCD ?codeSubstance_input ?quantiteSubstanceTotale_input ?unite
			WHERE {
			SERVICE <x-sparql-anything:location=work/2.csv,csv.headers=true> { 
			 	?line xyz:ID_patient ?ID_patient .
			 	?line xyz:ID_sejour ?ID_sejour .
			 	?line xyz:date ?date .
			 	?line xyz:heure ?heure .
			 	?line xyz:UCD ?UCD_string .
			 	?line xyz:labelUCD ?labelUCD .
			 	?line xyz:quantite ?quantite_string .
			 	?line xyz:unite_de_prise ?unite_de_prise .
			 	# bien utiliser le meme nom de variable que dans l'autre partie de la query
			 	?line xyz:codeSubstance ?codeSubstance_input .
			 	?line xyz:quantiteSubstanceTotale ?quantiteSubstanceTotale_string .
	 			?line xyz:unite ?unite .

			 	BIND(IRI(?UCD_string) AS ?UCD)
			 	BIND(xsd:decimal(?quantite_string) AS ?quantite)
			 	BIND(xsd:decimal(?quantiteSubstanceTotale_string) AS ?quantiteSubstanceTotale_input)
			}
		}
	}

	{
		# Lire le fichier de quantite de substance qui simule les dosages du RUIM
		# La jointure se fera sur UCD + codeSubstance
		SELECT ?codeSubstance_input ?code_substance_target ?conversion
			WHERE {
			SERVICE <x-sparql-anything:location=data/ConversionSUBSTANCE_DOSE.csv,csv.headers=true> { 
			 	# bien utiliser le meme nom de variable que dans l'autre partie de la query
			 	?line xyz:id%20substance ?codeSubstance_input .
			 	?line xyz:substance%20cible ?code_substance_target .
			 	?line xyz:conversion ?conversion_string .

			 	BIND(xsd:decimal(?conversion_string) AS ?conversion)
			}
		}
	}

	# Faire la multiplication entre la quantite de substance et la quantite administree
	# Ce qui donne la quantité totale de substance administrée
	BIND(?quantiteSubstanceTotale_input*COALESCE(?conversion,0) AS ?quantiteSubstanceTotale)
}