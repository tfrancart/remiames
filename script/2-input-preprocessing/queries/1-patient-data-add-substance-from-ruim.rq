PREFIX ruim: <http://data.esante.gouv.fr/ansm/medicament/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX r: <http://www.limics.org/remiames_ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?ID_patient ?ID_sejour ?date ?heure ?UCD ?quantite ?unite_de_prise ?labelUCD ?codeSubstance
WHERE{ 
	
	# Récupérer les susbtances contenues dans chaque UCD dans le RUIM
	# et leur libellé
	{
		SELECT ?UCD (STR(?labelUCD_lang) AS ?labelUCD) ?substance ?codeSubstance
		WHERE {
			?UCD rdfs:subClassOf ruim:UniteCommuneDeDispensation .
			?UCD rdfs:subClassOf ?specialite .
			?UCD rdfs:label ?labelUCD_lang .
			?specialite ruim:substanceActive|ruim:fractionTherapeutique ?substance .
			?substance dct:identifier ?codeSubstance .
		}
	}

	# Récupérer les administrations d'UCD
	{
		SELECT ?ID_patient ?ID_sejour ?date ?heure ?UCD ?quantite ?unite_de_prise
			WHERE {
			SERVICE <x-sparql-anything:location=input/patient-data.csv,csv.headers=true> { 
			 	?line xyz:ID_patient ?ID_patient .
			 	?line xyz:ID_sejour ?ID_sejour .
			 	?line xyz:date ?date .
			 	?line xyz:heure ?heure .
			 	?line xyz:UCD ?UCD_string .
			 	?line xyz:quantite ?quantite_string .
			 	?line xyz:unite_de_prise ?unite_de_prise .

			 	# Reconstruire la même URI que dans le RUIM
			 	# UCD est le critère de jointure
			 	BIND("http://data.esante.gouv.fr/ansm/medicament/" AS ?RUIM_NAMESPACE)
			 	BIND(IRI(CONCAT(?RUIM_NAMESPACE,"UCD_",STR(?UCD_string))) AS ?UCD)
			 	BIND(xsd:decimal(?quantite_string) AS ?quantite)
			}
		}
	}
}